apply plugin: 'maven-publish'

def isSnapshot = rootProject.version.endsWith( '-SNAPSHOT' )
def isRelease  = !isSnapshot

plugins.withType( JavaPlugin ) {
	java {
		withJavadocJar()
		withSourcesJar()
	}
	javadoc {
		failOnError = true

		options {
			source = sourceCompatibility
			memberLevel = 'PROTECTED'

			String javaApi = "https://docs.oracle.com/javase/${source.replace('1.', '')}/docs/api"

			links += [javaApi]
		}
	}
}

if( isRelease ) {
	tasks.withType( Jar ) {
		manifest {
			attributes(
					'Built-By'       : 'ascelion',
					'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
					'Build-Branch'   : scmVersion.scmPosition.branch,
					'Build-Commit'   : scmVersion.scmPosition.revision,
					'Build-Revision' : "${scmVersion.version}-${scmVersion.scmPosition.shortRevision}",
					'Created-By'     : "Gradle ${gradle.gradleVersion}",
					'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
					'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
					)
		}
	}
}

publishing {
	publications {
		maven( MavenPublication ) {
			if( project != rootProject ) {
				artifactId = "$rootProject.name-$project.name"
			}

			plugins.withType( JavaPlugin ) {
				from components.java
			}
			plugins.withType( DistributionPlugin ) {
				artifact distTar
				artifact distZip
			}
			pom {
				name = artifactId
				url = "${publishBase}/${publishName}"
				scm {
					url = "${publishBase}/${publishName}"
					connection = "${publishBase}/${publishName}.git"
					developerConnection = "${publishRepo}/${publishName}.git"
				}
				licenses {
					license {
						name = 'GNU Affero General Public License version 3'
						url = 'https://opensource.org/licenses/AGPL-3.0'
						distribution = 'repo'
					}
				}
				withXml {
					def xml = it.asNode()

					if( project == rootProject ) {
						def dm = xml.appendNode( 'dependencyManagement' ).appendNode( 'dependencies' )

						rootProject.subprojects { sub ->
							plugins.withType( MavenPublishPlugin ) {
								publishing {
									publications {
										all { pub ->
											def dep = dm.appendNode 'dependency'

											dep.appendNode 'groupId', pub.groupId
											dep.appendNode 'artifactId', pub.artifactId
											dep.appendNode 'version', pub.version
										}
									}
								}
							}
						}
					} else {
						def dm = xml.dependencyManagement.dependencies

						if( dm.size() > 0 ) {
							def dd = xml.dependencies

							dd.'*'
									.findAll { node -> node.version.text() == '' }
									.each { node ->
										def dep =  dm.'*'.find {
											node.groupId.text() == it.groupId.text() &&
													node.artifactId.text() == it.artifactId.text() &&
													node.classifier.text() == it.classifier.text()
										}

										node.appendNode( 'version', dep.version.text() )
									}

							xml.remove xml.dependencyManagement
						}
					}
				}
			}
		}
	}
}

def isExtended = gradle.metaClass.respondsTo( gradle, 'isPropertyPresent' )

if( isExtended ) {
	def forPublic  = project.hasProperty( 'forPublic' ) &&
					(project.getProperty( 'forPublic' ) == '' || project.getProperty( 'forPublic' ) )

	def signKey    = "${publishName}.sign.keyId"
	def signSecret = "${publishName}.sign.keySecret"
	def bintrayUSR = "${publishName}.bintray.username"
	def bintrayPWD = "${publishName}.bintray.password"

	publishing {
		if( forPublic && gradle.isPropertyPresent( bintrayUSR ) ) {
			repositories.maven {
				name = 'jfrog'
				url = "https://oss.jfrog.org/artifactory/oss-${isSnapshot ? 'snapshot' : 'release'}-local"

				credentials {
					username = gradle.mavenDecryptProperty( bintrayUSR )
					password = gradle.mavenDecryptProperty( bintrayPWD )
				}
			}
		}
	}

	if( isRelease && gradle.isPropertyPresent( signKey ) ) {
		apply plugin: 'signing'

		ext['signing.gnupg.keyName'] = gradle.mavenDecryptProperty( signKey )

		signing {
			useGpgCmd()
			sign publishing.publications.maven
		}
	}

	if( isRelease && forPublic && gradle.isPropertyPresent( bintrayUSR ) ) {
		apply plugin: 'com.jfrog.bintray'

		bintray {
			user = gradle.mavenDecryptProperty( bintrayUSR )
			key = gradle.mavenDecryptProperty( bintrayPWD )

			publications = ['maven']

			pkg {
				repo = 'repo'
				userOrg = 'ascelion'
				name = publishName
				licenses = ['AGPL-3.0']
				vcsUrl = "${publishBase}/${publishName}"
				version {
					name = rootProject.version

					gpg {
						sign = true
					}
				}
			}
		}
	}
}

