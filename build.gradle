buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.2'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
	}
}

apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
	ignoreUncommittedChanges = false
}

group = 'ascelion.flywaydb'
version = scmVersion.version

allprojects {
	apply plugin: 'lifecycle-base'
	apply plugin: 'eclipse'

	ext {
		publishName = 'flywaydb-ext'
		publishBase = 'https://github.com/ascelion'
		publishRepo = 'git@github.com:ascelion'
	}

	repositories {
		mavenLocal()
		jcenter()
	}

	clean {
		delete 'bin', 'target', '*.log'
	}

	defaultTasks 'build'
}

subprojects { sub ->
	group = rootProject.group
	version = rootProject.version

	apply plugin: 'java'

	sourceCompatibility = '8'
	targetCompatibility = '8'

	sourceSets {
		all {
			output.resourcesDir = output.classesDirs[0]
		}
	}
	tasks.withType( AbstractArchiveTask ) {
		duplicatesStrategy = 'exclude'
		archiveVersion = ''
	}
	plugins.withType( ApplicationPlugin ) {
		check.dependsOn run
		
		run {
			systemProperty 'force', UUID.randomUUID()
		}

		distTar.enabled = false
		distZip.enabled = false
	}
	configurations {
		flyway52
		flyway60
		flyway61
		flyway62
		flyway63
		flyway64
	}

	dependencies {
		flyway52 'org.flywaydb:flyway-core:5.2.4'
		flyway60 'org.flywaydb:flyway-core:6.0.8'
		flyway61 'org.flywaydb:flyway-core:6.1.3'
		flyway62 'org.flywaydb:flyway-core:6.2.4'
		flyway63 'org.flywaydb:flyway-core:6.3.3'
		flyway64 'org.flywaydb:flyway-core:6.4.4'

		constraints {
			implementation 'javax.enterprise:cdi-api:1.2'
			implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:2.0.2'

			implementation 'javax.annotation:javax.annotation-api:1.3'

			implementation 'org.slf4j:slf4j-api:1.7.28'
			implementation 'ch.qos.logback:logback-classic:1.2.3'

			implementation 'com.opencsv:opencsv:4.6'
			implementation 'com.h2database:h2:1.4.199'

			implementation 'ascelion.public:cdi-utils:0.2.1-SNAPSHOT'
		}

		testImplementation 'junit:junit:4.12'
		testImplementation 'org.hamcrest:hamcrest-core:1.3'
		testImplementation 'org.hamcrest:hamcrest-library:1.3'
	}

	eclipse.project.name = "$rootProject.name${project.path.replace(':', '-')}"
	eclipse.classpath.downloadSources = true
}

apply from: 'lombok.gradle'
apply from: 'publish.gradle'

